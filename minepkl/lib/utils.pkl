import "Recipe.pkl"
import "pkl:json" as json

const BlockColors = List(
  "white",
  "light_gray",
  "gray",
  "black",
  "brown",
  "red",
  "orange",
  "yellow",
  "lime",
  "green",
  "cyan",
  "light_blue",
  "blue",
  "purple",
  "magenta",
  "pink"
)

const VanillaWood = List(
  "oak",
  "spruce",
  "birch",
  "jungle",
  "acacia",
  "dark_oak",
  "mangrove",
  "cherry",
  "bamboo",
  "crimson",
  "warped"
)

function nameof(file: String) =
  file.split("/").last.replaceAll(".pkl", "")

function load(filename: String) =
  new json.Parser { useMapping = true }.parse(read(filename))

function pathof(id: String) =
  id.split(":").last

function namespaceof(id: String) =
  id.split(":").first

function coolItemInWater(input: String, output: String) = new Recipe.LycheeItemInside {
  local name = pathof(input)
  local source = namespaceof(input)

  id = "\(source):cool_heated_\(name)"

  item_in {
    item = input
  }

  block_in {
    blocks { "water" }
  }

  post {
    new {
      type = "drop_item"
      item = output
    }
    new {
      type = "execute"
      command = "playsound minecraft:entity.generic.extinguish_fire block @a ~ ~ ~"
      hide = true
    }
  }
}

function coolItemSplashing(input: String, output: String) = new Recipe.CreateSplashing {
  local name = pathof(input)
  local source = namespaceof(input)

  id = "\(source):splashing/cool_\(name)"

  ingredients {
    new { item = input }
  }

  results {
    new { item = output }
  }
}

function materialof(item: String) =
  if (item.contains("golden"))
    "gold"
  else
    pathof(item).split("_").first

function heatedpair(id) = new {
  new { item = id }
  new { item = "toolworking:heated_\(materialof(id))_ingot" }
}
