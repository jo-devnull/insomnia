import ".../lib/Recipe.pkl"
import ".../lib/utils.pkl"

local const Patterns {
  axe {
    "-- "
    "-~ "
    " / "
  }

  pickaxe {
    "---"
    " ~ "
    " / "
  }

  hoe {
    "-- "
    " ~ "
    " / "
  }

  shovel {
    " - "
    " ~ "
    " / "
  }

  sword {
    " - "
    " - "
    " / "
  }

  hammer {
    "---"
    "-~-"
    " / "
  }

  paxel {
    "---"
    "-~ "
    " / "
  }
}

local const SimplySwords {
  longsword {
    "/  "
    " - "
    "  -"
  }

  twinblade {
    "  -"
    " / "
    "-  "
  }

  rapier {
    "  -"
    " - "
    "/  "
  }

  sai {
    " -"
    "/ "
  }

  spear {
    "  -"
    " / "
    "/  "
  }

  glaive {
    "  -"
    " /-"
    "/  "
  }

  warglaive {
    " * "
    "-/-"
  }

  cutlass {
    " * "
    "-- "
    "/  "
  }

  claymore {
    " *-"
    "*-*"
    "/* "
  }

  greathammer {
    "---"
    "***"
    " / "
  }

  greataxe {
    "---"
    "*/*"
    " / "
  }

  chakram {
    "*-*"
    "- -"
    "*/*"
  }

  scythe {
    "--/"
    "-/ "
    "/  "
  }

  halberd {
    " -*"
    "-/-"
    "/  "
  }
}

local function heated(item: String) = "toolworking:heated_\(utils.pathof(item))"
local function nugget(item: String) = "\(utils.namespaceof(item)):\(utils.materialof(item))_nugget"

local function recipeFor(target, output, patt: Dynamic) = new Recipe.Shaped {
  id = if (target == "*") output else target
  pattern = patt.toList().toListing()

  key {
    ["-"] { item = "toolworking:heated_\(utils.materialof(output))_ingot" }
    ["/"] { item = "minecraft:stick" }

    when (pattern.any((s) -> s.contains("*"))) {
      ["*"] { item = nugget(output) }
    }

    when (pattern.any((s) -> s.contains("~"))) {
      ["~"] { tag = "forge:string" }
    }
  }

  result {
    item = output
  }
}

local function toDiamond(target, input: String, output: String) = new Recipe.Shaped {
  id = if (target == "*") output else target

  key {
    ["#"] { item = "minecraft:diamond" }
    ["X"] { item = input }
  }

  pattern {
    "###"
    "#X#"
    "###"
  }

  result { item = output }
}

tools {
  recipeFor("*", "create_ironworks:copper_axe",     Patterns.axe)
  recipeFor("*", "create_ironworks:copper_hoe",     Patterns.hoe)
  recipeFor("*", "create_ironworks:copper_shovel",  Patterns.shovel)
  recipeFor("*", "create_ironworks:copper_sword",   Patterns.sword)
  recipeFor("*", "create_ironworks:copper_pickaxe", Patterns.pickaxe)
  recipeFor("*", "create_ironworks:copper_hammer",  Patterns.hammer)
  recipeFor("*", "create_ironworks:copper_paxel",   Patterns.paxel)

  recipeFor("*", "create_ironworks:bronze_axe",     Patterns.axe)
  recipeFor("*", "create_ironworks:bronze_hoe",     Patterns.hoe)
  recipeFor("*", "create_ironworks:bronze_shovel",  Patterns.shovel)
  recipeFor("*", "create_ironworks:bronze_sword",   Patterns.sword)
  recipeFor("*", "create_ironworks:bronze_pickaxe", Patterns.pickaxe)
  recipeFor("*", "create_ironworks:bronze_hammer",  Patterns.hammer)
  recipeFor("*", "create_ironworks:bronze_paxel",   Patterns.paxel)

  recipeFor("*", "create_ironworks:brass_axe",      Patterns.axe)
  recipeFor("*", "create_ironworks:brass_hoe",      Patterns.hoe)
  recipeFor("*", "create_ironworks:brass_shovel",   Patterns.shovel)
  recipeFor("*", "create_ironworks:brass_sword",    Patterns.sword)
  recipeFor("*", "create_ironworks:brass_pickaxe",  Patterns.pickaxe)
  recipeFor("*", "create_ironworks:brass_hammer",   Patterns.hammer)
  recipeFor("*", "create_ironworks:brass_paxel",    Patterns.paxel)

  recipeFor("*", "create_ironworks:steel_axe",      Patterns.axe)
  recipeFor("*", "create_ironworks:steel_hoe",      Patterns.hoe)
  recipeFor("*", "create_ironworks:steel_shovel",   Patterns.shovel)
  recipeFor("*", "create_ironworks:steel_sword",    Patterns.sword)
  recipeFor("*", "create_ironworks:steel_pickaxe",  Patterns.pickaxe)
  recipeFor("*", "create_ironworks:steel_hammer",   Patterns.hammer)
  recipeFor("*", "create_ironworks:steel_paxel",    Patterns.paxel)

  recipeFor("*", "minecraft:iron_axe",              Patterns.axe)
  recipeFor("*", "minecraft:iron_hoe",              Patterns.hoe)
  recipeFor("*", "minecraft:iron_sword",            Patterns.sword)
  recipeFor("*", "minecraft:iron_shovel",           Patterns.shovel)
  recipeFor("*", "minecraft:iron_pickaxe",          Patterns.pickaxe)
  recipeFor("*", "create_ironworks:iron_paxel",     Patterns.paxel)
  recipeFor("*", "create_ironworks:iron_hammer",    Patterns.hammer)

  recipeFor("*", "minecraft:golden_axe",            Patterns.axe)
  recipeFor("*", "minecraft:golden_hoe",            Patterns.hoe)
  recipeFor("*", "minecraft:golden_sword",          Patterns.sword)
  recipeFor("*", "minecraft:golden_shovel",         Patterns.shovel)
  recipeFor("*", "minecraft:golden_pickaxe",        Patterns.pickaxe)
  recipeFor("*", "create_ironworks:gold_paxel",     Patterns.paxel)
  recipeFor("*", "create_ironworks:gold_hammer",    Patterns.hammer)

  toDiamond("*", "create_ironworks:steel_axe",     "minecraft:diamond_axe")
  toDiamond("*", "create_ironworks:steel_hoe",     "minecraft:diamond_hoe")
  toDiamond("*", "create_ironworks:steel_sword",   "minecraft:diamond_sword")
  toDiamond("*", "create_ironworks:steel_shovel",  "minecraft:diamond_shovel")
  toDiamond("*", "create_ironworks:steel_pickaxe",  "minecraft:diamond_pickaxe")
  toDiamond("*", "create_ironworks:steel_hammer",  "create_ironworks:diamond_hammer")
  toDiamond("*", "create_ironworks:steel_paxel",   "create_ironworks:diamond_paxel")

  local sawPattern = new {"  /" " /-" "/  "}
  local filletKnifePattern = new {"  -" " - " "/  "}
  local cleaverPattern = new {"--" "-/"}
  local knifePattern = new {"-" "/"}

  recipeFor("*", "notreepunching:iron_saw", sawPattern)
  toDiamond("*", "notreepunching:iron_saw", "notreepunching:diamond_saw")

  recipeFor("*", "aquaculture:iron_fillet_knife", filletKnifePattern)
  recipeFor("*", "aquaculture:gold_fillet_knife", filletKnifePattern)
  toDiamond("*", "aquaculture:iron_fillet_knife", "aquaculture:diamond_fillet_knife")

  recipeFor("*", "dungeonsdelight:iron_cleaver", cleaverPattern)
  recipeFor("*", "dungeonsdelight:golden_cleaver", cleaverPattern)
  toDiamond("*", "dungeonsdelight:iron_cleaver", "dungeonsdelight:diamond_cleaver")

  recipeFor("*", "farmersdelight:iron_knife", knifePattern)
  recipeFor("*", "farmersdelight:golden_knife", knifePattern)
  toDiamond("*", "farmersdelight:iron_knife", "farmersdelight:diamond_knife")
}

weapons {
  for (tool, patt in SimplySwords.toMap()) {
    for (material in List("gold", "iron")) {
      recipeFor("*", "simplyswords:\(material)_\(tool)", patt)
    }

    toDiamond("*", "simplyswords:iron_\(tool)", "simplyswords:diamond_\(tool)")
  }
}

disabled {
  for (material in List("diamond", "iron", "gold", "copper", "bronze", "brass", "steel")) {
    for (tool, _ in Patterns.toMap()) {
      new Recipe.Empty { id = "create_ironworks:compat/\(material)_\(tool)" }
      new Recipe.Empty { id = "create_ironworks:tools/\(tool)/\(material)" }
    }
  }
}
