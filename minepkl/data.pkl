import "lib/Recipe.pkl"
import "toolworking/toolworking.pkl"
import "misc/fishing.pkl"
import "tags/misc.pkl" as tags
import* "recipes/*.pkl" as recipes
import "misc/replace.pkl" as replacements
import "tags/category/@category.pkl" as categoryTags

const jsonRenderer = new JsonRenderer {}

function getPath(path: String, type: String) =
  if (path.contains(":")) path
  else "modpack:\(type)/\(path)"

function buildRecipes() = new Mapping<String, FileOutput> {
  for (_, mod in recipes.toMap()) {
    for (key, recipe in mod.toMap()) {
      [getPath(key, "recipes")] {
        value = recipe
        renderer = jsonRenderer
      }
    }
  }

  for (_, list in import("recipes/remove.pkl").toMap()) {
    for (id in list) {
      [Recipe.empty(id).getPath()] {
        value = Recipe.empty(id)
        renderer = jsonRenderer
      }
    }
  }
}

function buildCategoryTags() = new Mapping<String, FileOutput> {
  for (key, tag in categoryTags.toMap()) {
    ["category:tags/items/\(key)"] {
      value {
        replace = false
        values = new Listing<String> {
          for (modname, list in tag.mod) {
            for (item in list) {
              if (item.contains(":")) item else "\(modname):\(item)"
            }
          }
        }
      }

      renderer = jsonRenderer
    }
  }
}

function buildTags() = new Mapping<String, FileOutput> {
  for (key, tag in tags.toMap()) {
    [getPath(key, "tags")] {
      value = tag
      renderer = jsonRenderer
    }
  }
}

function buildReplacements() = new Mapping<String, FileOutput> {
  ["oei:replacements/mod_replacements"] {
    value {
      for (key, val in replacements.toMap()) {
        for (item in val.items) {
          when (item is String) {
            new {
              matchItems { "\(val.from):\(item)" }
              resultItems = "\(val.to):\(item)"
            }
          } else {
            new {
              matchItems { "\(val.from):\(item[0])" }
              resultItems = "\(val.to):\(item[1])"
            }
          }
        }
      }
    }

    renderer = jsonRenderer
  }
}

function buildFishing() = new Mapping<String, FileOutput> {
  for (mod, map in fishing.toMap()) {
    for (key, val in map) {
      ["\(mod):fishing/\(key)"] {
        value {
          input { item = "\(mod):\(key)" }
          result { id = if (val == "*") "\(mod):\(key)" else "\(mod):\(val)" }
        }

        renderer = jsonRenderer
      }
    }
  }
}

output {
  files {
    ...buildTags()
    ...buildCategoryTags()
    ...buildRecipes()
    ...buildReplacements()
    ...buildFishing()
    ...toolworking.build()
  }
}
