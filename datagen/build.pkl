import "./tags/@tags.pkl" as tags
import "./utils.pkl" as utils

const path {
  data = "./resourcepacks/@insomnia/data"
  assets = "./resourcepacks/@insomnia/assets"
}

const json: FileOutput = new {
  value = null
  renderer = new JsonRenderer {}
}

function buildEmi() = new Mapping<String, FileOutput> {
  ["emi.json"] = (json) {
    value {
      hidden_stacks = utils.load("../emi.json")
        .getOrNull("hidden_stacks")

      favorites = new Listing {
        for (tag in tags.toMap().keys) {
          new { stack = "#item:category:\(tag)" }
        }
      }
    }
  }
}

function buildLang() = new Mapping<String, FileOutput> {
  ["\(path.assets)/category/lang/pt_br.json"] = (json) {
    value {
      for (key, tag in tags.toMap()) {
        ["tag.item.category.\(key)"] = tag.title
      }
    }
  }
}

function buildTags() = new Mapping<String, FileOutput> {
  for (key, tag in tags.toMap()) {
    ["\(path.data)/category/tags/items/\(key).json"] = (json) {
      value {
        replace = false
        values = new Listing<String> {
          for (modname, list in tag.mod) {
            for (item in list) {
              if (item.contains(":")) item else "\(modname):\(item)"
            }
          }
        }
      }
    }

    ["\(path.assets)/category/models/tag/item/\(key).json"] = (json) {
      value { parent = tag.icon }
    }
  }
}

function buildMisc() = new Mapping<String, FileOutput> {
  for (key, tag in import("misc/tags.pkl").toMap()) {
    ["\(path.data)/modpack/tags/\(tag.path).json"] = (json) {
      value {
        replace = false
        values = tag.values
      }
    }
  }
}

function buildRecipes() = new Mapping<String, FileOutput> {
  for (_, mod in import*("recipes/*.pkl").toMap()) {
    for (key, recipe in mod.toMap()) {
      when (key.contains("/")) {
        ["./resourcepacks/@insomnia/\(key)"] = (json) {
          value = recipe
        }
      } else {
        ["\(path.data)/modpack/recipes/\(key).json"] = (json) {
          value = recipe
        }
      }
    }
  }
}

output {
  files {
    ...buildEmi()
    ...buildTags()
    ...buildLang()
    ...buildMisc()
    ...buildRecipes()

    ["./config/incontrol/spawn.json"] = (json) {
      value = import("misc/spawn.pkl").rules
    }

    ["./config/incontrol/phases.json"] = (json) {
      value = import("misc/spawn.pkl").phases
    }

    ["./resourcepacks/@insomnia/pack.mcmeta"] = (json) {
      value {
        pack {
          pack_format = 15
          description = "Data & Assets for the modpack"
        }
      }
    }
  }
}
